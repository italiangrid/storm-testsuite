*** Setting ***

Library  OperatingSystem
Library  Collections
Resource  lib/stormlib.txt

*** Keywords ***

Create voms proxy   [Arguments]   ${vo}=${VO1}
  ${output}  ${stderr}  Execute and Check Success   echo ${privateKeyPassword}|voms-proxy-init -pwstdin --voms ${vo}
  Log  ${output}
  Log  ${stderr}

Create plain proxy
  ${output}  ${stderr}  Execute and Check Success   echo ${privateKeyPassword} | voms-proxy-init -pwstdin
  Log  ${output}
  Log  ${stderr}

Clear all credentials
  Stop using certificate
  Set Environment Variable  X509_USER_PROXY  ${DEFAULT_X509_USER_PROXY}

Use certificate   [Arguments]   ${cert}
  File Should Exist   ${certsDir}/${cert}.cert.pem
  File Should Exist   ${certsDir}/${cert}.key.pem
  Stop using certificate
  Execute and Check Success   cp ${certsDir}/${cert}.cert.pem %{HOME}/.globus/usercert.pem
  Execute and Check Success   cp ${certsDir}/${cert}.key.pem %{HOME}/.globus/userkey.pem
  Execute and Check Success   chmod 400 %{HOME}/.globus/userkey.pem

Stop using certificate
  Run  rm ${usercert}
  Run  rm -f ${userkey}

Use proxy  [Arguments]  ${proxypath}
  File Should Exist   ${proxypath}
  Set Environment Variable  X509_USER_PROXY  ${proxypath}

Use certificate and proxy  [Arguments]  ${user}  ${proxypath}
  Use certificate  ${user}
  Use proxy  ${proxypath}