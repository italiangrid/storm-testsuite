** Settings ***

Library   OperatingSystem
Resource   variables.txt

*** Keywords ***

Execute and Check Success  [Arguments]  ${cmd}
  ${rc}  ${output}=  Run and Return RC And Output  ${cmd}
  Should Be Equal As Integers  ${rc}  0  ${output}  False
  [Return]  ${output}

Get a unique name
  ${name}  Execute and Check Success  echo $RANDOM
  [Return]  ${name}

Create local file
  ${name}  Get a unique name
  Execute and Check Success  dd if=/dev/urandom of=/tmp/${name} bs=1M count=1
  [Return]  ${name}

Execute clientSRM Command  [Arguments]  ${cmd}
  ${output}  Execute and Check Success  clientSRM ${cmd} -e ${srmEndpoint}
  [Return]  ${output}

Execute clientSRM Command on Surl  [Arguments]  ${cmd}  ${surl}
  ${output}  Execute and Check Success  clientSRM ${cmd} -e ${srmEndpoint} -s ${surl}
  [Return]  ${output}

Execute Curl  [Arguments]  ${cmd}
  ${output}  Execute and Check Success  curl --cacert ${usercert} --cert ${userproxy} --capath ${trustdir} ${cmd}
  [Return]  ${output}

Create directory using clientSRM  [Arguments]  ${dirname}
  ${output}  Execute clientSRM Command on Surl  mkdir  srm://${srmEndpoint}/${root}/${dirname}
  Should Contain  ${output}  SRM_SUCCESS

Remove directory using clientSRM  [Arguments]  ${dirname}
  ${output}  Execute clientSRM Command on Surl  rmdir  srm://${srmEndpoint}/${root}/${dirname}
  Should Contain  ${output}  SRM_SUCCESS

Prepare to put  [Arguments]  ${filename}
  ${output}  Execute clientSRM Command on Surl  ptp -p  srm://${srmEndpoint}/${root}/${filename}
  Should Contain  ${output}  SRM_SUCCESS
  ${result}  ${token}=  Should Match Regexp  ${output}  requestToken=(\".+\")
  [Return]  ${token}

Put without really putting  [Arguments]  ${filename}
	${token}  Prepare to put  ${filename}
	${output}  Execute clientSRM Command on Surl  pd -p  srm://${srmEndpoint}/${root}/${test_file} -t ${token}
  Should Contain  ${output}  SRM_SUCCESS

Prepare to get  [Arguments]  ${filename}
  ${output}  Execute clientSRM Command on Surl  ptg -p  srm://${srmEndpoint}/${root}/${test_file}
  Should Contain  ${output}  SRM_SUCCESS
  ${result}  ${token}=  Should Match Regexp  ${output}  requestToken=(\".+\")
  [Return]  ${token}

Get unused size  [Arguments]  ${storageAreaToken}
  ${output}  Execute clientSRM Command  gst -e httpg://${srmEndpoint}/ -d ${storageAreaToken}
  ${result}  ${token}=  Should Match Regexp  ${output}  (\".+-.+-.+-.+-.+\")
  ${output}  Execute clientSRM Command  gsm -e httpg://${srmEndpoint}/ -s ${token}
  ${result}  ${unusedSize}=  Should Match Regexp  ${output}  unusedSize=(.+)
  [Return]  ${unusedSize}

Check file exists using lcg-utils  [Arguments]  ${path}
  Execute and Check Success  lcg-ls -l -b -D srmv2 srm://${srmEndpoint}/srm/managerv2?SFN=/${root}/${path}

Check file does not exists using lcg-utils  [Arguments]  ${path}
  ${output}  Run  lcg-ls -l -b -D srmv2 srm://${srmEndpoint}/srm/managerv2?SFN=/${root}/${path}
  Should Contain  ${output}  No such file or directory

Copy file using lcg-utils  [Arguments]  ${localPath}  ${remotePath}
  Execute and Check Success  lcg-cp -b --verbose -D srmv2 file:///tmp/${localPath} srm://${srmEndpoint}/srm/managerv2?SFN=/${root}/${remotePath}
