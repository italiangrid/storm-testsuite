** Settings ***

Library   OperatingSystem
Resource   variables.txt

*** Keywords ***

Execute and Check Success  [Arguments]  ${cmd}
  ${rc}  ${output}=  Run and Return RC And Output  ${cmd}
  Should Be Equal As Integers  ${rc}  0  ${output}  False
  [Return]  ${output}

Execute and Check Failure   [Arguments]   ${cmd}
  ${rc}   ${output}=   Run and Return RC And Output  ${cmd}
  Should Not Be Equal As Integers  ${rc}  0  ${output}
  [Return]  ${output}

Get a unique name
  ${name}  Execute and Check Success  basename `mktemp`
  [Return]  ${name}

Create local file
  ${name}  Get a unique name
  Execute and Check Success  dd if=/dev/urandom of=/tmp/${name} bs=1M count=1
  [Return]  ${name}

Create local file with fake size  [Arguments]  ${megabytes}
  ${name}  Get a unique name
  Execute and Check Success  dd if=/dev/null of=/tmp/${name} bs=1M count=1 seek=${megabytes}
  [Return]  ${name}

Create local empty file
  ${name}  Get a unique name
  Execute and Check Success  touch /tmp/${name}
  [Return]  ${name}

Remove local file  [Arguments]  ${filename}
  Execute and Check Success  rm -f /tmp/${filename}

Remove local directory  [Arguments]  ${dirname}
  Execute and Check Success  rm -rf /tmp/${dirname}

Create local file with checksum that starts with zero
  ${name}  Get a unique name
  Execute and Check Success  echo "a" > /tmp/${name}
  [Return]  ${name}

Execute Curl with proxy and options  [Arguments]  ${url}  ${options}=${EMPTY}
  [Documentation]  This keyword should be used when invoking CURL with a proxy certificate
  ${output}  Execute and Check Success  curl --cert ${userproxy} --cacert ${usercert} --capath ${trustdir} ${options} ${url}
  [Return]  ${output}

Execute Curl with certificate and options  [Arguments]  ${url}  ${options}=${EMPTY}
  [Documentation]  This keyword should be used when invoking CURL with an EEC
  ${output}  Execute and Check Success  curl --cert ${usercert} --key ${userkey} --capath ${trustdir} ${options} ${url}
  [Return]  ${output}

Execute Curl with options   [Arguments]  ${url}  ${options}=${EMPTY}
  ${output}  Execute and Check Success  curl ${options} ${url}
  [Return]   ${output} 

Build surl  [Arguments]  ${storageArea}  ${relativePath}
  ${output}  Set variable  srm://${srmEndpoint}/srm/managerv2?SFN=/${storageArea}/${relativePath}
  [Return]  ${output}

Build simple surl  [Arguments]  ${storageArea}  ${relativePath}
  ${output}  Set variable  srm://${srmEndpoint}/${storageArea}/${relativePath}
  [Return]  ${output}

Build file transfer TURL  [Arguments]  ${storageArea}  ${relativePath}
  ${output}  Set variable  http://${filetransferEndpoint}/fileTransfer/${storageArea}/${relativePath}
  [Return]  ${output}

Build file transfer secure TURL  [Arguments]  ${storageArea}  ${relativePath}
  ${output}  Set variable  https://${filetransferSecureEndpoint}/fileTransfer/${storageArea}/${relativePath}
  [Return]  ${output}

Create local symlink  [Arguments]  ${linkTo}  ${linkDirectory}
  Execute and Check Success  ln -s ${linkTo} -t ${linkDirectory}