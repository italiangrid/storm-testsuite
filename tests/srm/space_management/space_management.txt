*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/lcg_util.txt
Resource   lib/clientSRM.txt

*** Test Cases ***

Space token used space is update
  [Documentation]  Regression test for https://storm.cnaf.infn.it:8443/redmine/issues/109. Given a Space Token ST and a SURL that resides on ST pointing to a not existent file, verify that inspecting the unused space of the ST before and after a non-empty file has been stored on the SURL, the ST used space value is updated accordingly to the size of the new file.
  [Tags]  storm-client  lcg-utils  gst  regression
  ${size_before}  Get unused size  ${storageAreaToken}
  Log  ${size_before}
  ${filename}  Create local file
  Copy-out file using lcg-utils  ${filename}  ${filename}
  ${size_after}  Get unused size  ${storageAreaToken}
  Log  ${size_after}
  ${diff_size}  Run  echo `expr ${size_before} - ${size_after}`
  ${file_size}  Run  ls -l /tmp/${filename} | awk {'print $5'}
  Should be equal  ${file_size}  ${diff_size}

Check a srmGetSpaceMetadata call succedes when a valid Space Token not associated to a Storage Area is given
  [Tags]  storm-client  gsm  regression
  [Documentation]  Regression test for https://storm.cnaf.infn.it:8443/redmine/issues/277 Storm failed with "SRM_INTERNAL_ERROR" and "Storage Space not initialized yet" as explaination. It must succede since it does not needs to be initialized.
  ${token}  Reserve space
  ${output}  Get space metadata  ${token}
  Should Contain  ${output}  SRM_SUCCESS  
  ${output}  Release space  ${token}
  Should Contain  ${output}  SRM_SUCCESS

Check a GetSpaceMetadata succedes when using a proxy without voms extensions
  [Tags]  storm-client  gsm  regression
  [Documentation]  Regression test for https://storm.cnaf.infn.it:8443/redmine/issues/189
  ${token}  Get space token  
  ${output}  Get space metadata  ${token}
  Should Contain  ${output}  SRM_SUCCESS
