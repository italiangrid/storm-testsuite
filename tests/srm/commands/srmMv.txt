*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt
Resource   lib/lcg_util.txt

*** Test Cases ***

Move existent file
  [Tags]  storm-client  mv
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${srcsurl}  Build surl  ${vo}  ${filename}
  ${destsurl}  Build surl  ${vo}  ${filename}.moved
  Copy-out file using lcg-utils  ${filename}  ${srcsurl}
  ${output}  Perform mv using clientSRM  ${srcsurl}  ${destsurl}
  Should Contain  ${output}  SRM_SUCCESS
  Remove file using clientSRM  ${vo}  ${filename}.moved
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Move existent not empty directory
  [Tags]  storm-client  mv
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${filename}  Create local file
  ${dirsurl}  Build surl  ${vo}  ${dirname}
  ${filesurl}  Build surl  ${vo}  ${dirname}/${filename}
  ${destdirsurl}  Build surl  ${vo}  ${dirname}.moved
  ${output}  Perform mkdir using clientSRM  ${dirsurl}
  Should Contain  ${output}  SRM_SUCCESS
  Copy-out file using lcg-utils  ${filename}  ${filesurl}
  ${output}  Perform mv using clientSRM  ${dirsurl}  ${destdirsurl}
  Should Contain  ${output}  SRM_SUCCESS
  Remove not empty directory using clientSRM  ${vo}  ${dirname}.moved
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Move file to an existent file
  [Tags]  storm-client  mv
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${srcsurl}  Build surl  ${vo}  ${filename}
  ${destsurl}  Build surl  ${vo}  ${filename}.moved
  Copy-out file using lcg-utils  ${filename}  ${srcsurl}
  Copy-out file using lcg-utils  ${filename}  ${destsurl}
  ${output}  Perform mv using clientSRM  ${srcsurl}  ${destsurl}
  Should Contain  ${output}  SRM_DUPLICATION_ERROR
  Remove file using clientSRM  ${vo}  ${filename}
  Remove file using clientSRM  ${vo}  ${filename}.moved
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Move directory to an existent directory
  [Tags]  storm-client  mv
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${filename}  Create local file
  ${dirsurl}  Build surl  ${vo}  ${dirname}
  ${filesurl}  Build surl  ${vo}  ${dirname}/${filename}
  ${destdirsurl}  Build surl  ${vo}  ${dirname}.moved
  ${destfilesurl}  Build surl  ${vo}  ${dirname}.moved/${filename}
  ${output}  Perform mkdir using clientSRM  ${dirsurl}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform mkdir using clientSRM  ${destdirsurl}
  Should Contain  ${output}  SRM_SUCCESS
  Copy-out file using lcg-utils  ${filename}  ${filesurl}
  Copy-out file using lcg-utils  ${filename}  ${destfilesurl}
  ${output}  Perform mv using clientSRM  ${dirsurl}  ${destdirsurl}
  Should Contain  ${output}  SRM_DUPLICATION_ERROR
  Remove not empty directory using clientSRM  ${vo}  ${dirname}
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Move file over itself
  [Tags]  storm-client  mv
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${srcsurl}  Build surl  ${vo}  ${filename}
  ${destsurl}  Build surl  ${vo}  ${filename}
  Copy-out file using lcg-utils  ${filename}  ${srcsurl}
  ${output}  Perform mv using clientSRM  ${srcsurl}  ${destsurl}
  Should Contain  ${output}  SRM_SUCCESS
  Remove file using clientSRM  ${vo}  ${filename}
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Move file with active ptg
  [Tags]  storm-client  mv
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${srcsurl}  Build surl  ${vo}  ${filename}
  ${destsurl}  Build surl  ${vo}  ${filename}.moved
  Copy-out file using lcg-utils  ${filename}  ${srcsurl}
  ${output}  ${token}  Perform ptg using clientSRM  ${srcsurl}  -p
  Should Contain  ${output}  SRM_FILE_PINNED
  ${output}  Perform mv using clientSRM  ${srcsurl}  ${destsurl}
  Should Contain  ${output}  SRM_FILE_BUSY
  ${output}  Perform rf using clientSRM  ${srcsurl}  ${token}
  Should Contain  ${output}  SRM_SUCCESS
  Remove file using clientSRM  ${vo}  ${filename}
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Move file with active ptp
  [Tags]  storm-client  mv
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Get a unique name
  ${srcsurl}  Build surl  ${vo}  ${filename}
  ${destsurl}  Build surl  ${vo}  ${filename}.moved
  ${output}  ${token}  Perform ptp using clientSRM  ${srcsurl}  -p
  Should Contain  ${output}  SRM_SPACE_AVAILABLE
  ${output}  Perform mv using clientSRM  ${srcsurl}  ${destsurl}
  Should Contain  ${output}  SRM_FILE_BUSY
  ${output}  Perform pd using clientSRM  ${srcsurl}  ${token}
  Should Contain  ${output}  SRM_SUCCESS
  Remove file using clientSRM  ${vo}  ${filename}
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Move file on an unauthorized destination
  [Tags]  storm-client  mv  to-be-fixed
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${srcsurl}  Build surl  ${vo}  ${filename}
  ${destsurl}  Build surl  ${vo}  ${filename}
  Copy-out file using lcg-utils  ${filename}  ${srcsurl}
  Init certificate and plain proxy  test0
  ${output}  Perform mv using clientSRM  ${srcsurl}  ${destsurl}
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  Init certificate and voms proxy  test0  ${vo}
  Remove file using clientSRM  ${vo}  ${filename}
  Remove local file  ${filename}
  [Teardown]  Clear all credentials