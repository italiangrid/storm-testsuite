*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

*** Test Cases ***

Remove existent file
  [Tags]  storm-client  rm
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Get a unique name
  ${surl}  Build surl  ${vo}  ${filename}
  Put without really putting using clientSRM  ${vo}  ${filename}
  ${output}  Perform rm using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Remove unexistent file
  [Tags]  storm-client  rm
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Get a unique name
  ${surl}  Build surl  ${vo}  ${filename}
  ${output}  Perform rm using clientSRM  ${surl}
  Should Contain  ${output}  SRM_INVALID_PATH
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Removing unauthorized file
  [Tags]  storm-client  rm  to-be-fixed
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Get a unique name
  ${surl}  Build surl  ${vo}  ${filename}
  Put without really putting using clientSRM  ${vo}  ${filename}
  Init certificate and plain proxy  test0
  ${output}  Perform rm using clientSRM  ${surl}
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  Init certificate and voms proxy  test0  ${vo}
  ${output}  Perform rm using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Partial success on removing multiple files
  [Tags]  storm-client  rm
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Get a unique name
  ${filename2}  Get a unique name
  Put without really putting using clientSRM  ${vo}  ${filename}
  Put without really putting using clientSRM  ${vo}  ${filename2}
  ${surl}  Build surl  ${vo}  ${filename}
  ${surl2}  Build surl  ${vo}  ${filename2}
  ${output}  Perform rm using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform rm using clientSRM  ${surl}, ${surl2}
  Should Contain  ${output}  SRM_PARTIAL_SUCCESS
  Remove local file  ${filename}
  Remove local file  ${filename2}
  [Teardown]  Clear all credentials

Remove a pinned file must set aborted the ptg request
  [Tags]  storm-client  rm  ptg
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Get a unique name
  Put without really putting using clientSRM  ${vo}  ${filename}
  ${surl}  Build surl  ${vo}  ${filename}
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_FILE_PINNED
  ${output}  Perform rm using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform sptg using clientSRM  ${surl}  ${token}
  Should Not Contain  ${output}  SRM_FILE_PINNED
  Should Contain  ${output}  SRM_ABORTED
  Remove local file  ${filename}
  [Teardown]  Clear all credentials