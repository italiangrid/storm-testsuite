*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

Resource   lib/setup.txt

Suite Setup       Setup suite if needed
Suite Teardown    Teardown suite if needed

*** Test Cases ***

Create new empty directory
  [Tags]  storm-client  mkdir
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${dirname}  Get a unique name
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${TESTDIR}/${dirname}
  ${output}  Perform mkdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform rmdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  [Teardown]  Clear all credentials

Create directory with invalid path
  [Tags]  storm-client  mkdir
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${dirname}  Get a unique name
  ${surl}  Build surl  fakeVO  ${dirname}
  ${output}  Perform mkdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_INVALID_PATH
  [Teardown]  Clear all credentials

Create directory that already exists
  [Tags]  storm-client  mkdir
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${dirname}  Get a unique name
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${TESTDIR}/${dirname}
  ${output}  Perform mkdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform mkdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_DUPLICATION_ERROR
  Should Contain  ${output}  Directory specified exists!
  ${output}  Perform rmdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  [Teardown]  Clear all credentials

Create directory with unauthorized credentials
  [Tags]  storm-client  mkdir
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${dirname}  Get a unique name
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${TESTDIR}/${dirname}
  ${output}  Perform mkdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  [Teardown]  Clear all credentials

Check directory creation on a different VO
  [Tags]  storm-client  ls
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${dirname}  Get a unique name
  ${qsurl}  Build surl  ${VOMSAUTH_SA2}  ../${VOMSAUTH_SA}/${TESTDIR}/${dirname}
  ${output}  Perform mkdir using clientSRM  ${qsurl}
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  ${qsurl}  Build surl  ${VOMSAUTH_SA}  ../${VOMSAUTH_SA2}/${TESTDIR}/${dirname}
  ${output}  Perform mkdir using clientSRM  ${qsurl}
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  ${qsurl}  Build surl  ${VOMSAUTH_SA}  ../${X509AUTH_SA}/${TESTDIR}/${dirname}
  ${output}  Perform mkdir using clientSRM  ${qsurl}
  Should Contain  ${output}  SRM_SUCCESS
  [Teardown]  Clear all credentials
