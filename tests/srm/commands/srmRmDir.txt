*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

*** Test Cases ***

Remove existent empty directory
  [Tags]  storm-client  rmdir
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${surl}  Build surl  ${vo}  ${dirname}
  ${output}  Perform mkdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform rmdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  Remove local directory  ${dirname}
  [Teardown]  Clear all credentials

Remove unexistent directory
  [Tags]  storm-client  mkdir
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${surl}  Build surl  ${vo}  ${dirname}
  ${output}  Perform rmdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_INVALID_PATH
  Remove local directory  ${dirname}
  [Teardown]  Clear all credentials

Remove existent non empty directory without recursion
  [Tags]  storm-client  rmdir
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${filename}  Get a unique name
  ${dirsurl}  Build surl  ${vo}  ${dirname}
  ${output}  Perform mkdir using clientSRM  ${dirsurl}
  Should Contain  ${output}  SRM_SUCCESS
  Put without really putting using clientSRM  ${vo}  ${dirname}/${filename}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform rmdir using clientSRM  ${dirsurl}
  Should Contain  ${output}  SRM_NON_EMPTY_DIRECTORY
  Remove not empty directory using clientSRM  ${vo}  ${dirname}
  Remove local directory  ${dirname}
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Remove existent non empty directory with recursion
  [Tags]  storm-client  rmdir
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${filename}  Get a unique name
  ${dirsurl}  Build surl  ${vo}  ${dirname}
  ${output}  Perform mkdir using clientSRM  ${dirsurl}
  Should Contain  ${output}  SRM_SUCCESS
  Put without really putting using clientSRM  ${vo}  ${dirname}/${filename}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform rmdir using clientSRM  ${dirsurl}  -r 1
  Should Contain  ${output}  SRM_SUCCESS
  Remove not empty directory using clientSRM  ${vo}  ${dirname}
  Remove local directory  ${dirname}
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Remove invalid path
  [Tags]  storm-client  rmdir
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${surl}  Build surl  fakeVO  ${dirname}
  ${output}  Perform rmdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_INVALID_PATH
  Remove local directory  ${dirname}
  [Teardown]  Clear all credentials

Removing unauthorized directory
  [Tags]  storm-client  rmdir  to-be-fixed
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${surl}  Build surl  ${vo}  ${dirname}
  ${output}  Perform mkdir using clientSRM  ${surl}
  Init certificate and plain proxy  test0
  ${output}  Perform rmdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  Init certificate and voms proxy  test0  ${vo}
  ${output}  Perform rmdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  Remove local directory  ${dirname}
  [Teardown]  Clear all credentials
