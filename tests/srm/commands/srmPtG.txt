*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt
Resource   lib/lcg_util.txt
Resource   lib/globus_util.txt

*** Test Cases ***

Prepare to get an existent file
  [Tags]  storm-client  ptg
  [Setup]  Init certificate and voms proxy  test0  ${VOMSAUTH_SA_VONAME}
  ${filename}  Get a unique name
  Put without really putting using clientSRM  ${VOMSAUTH_SA}  ${filename}
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${filename}
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}
  Should Contain  ${output}  SRM_REQUEST_QUEUED
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -t ${token} -p
  Should Contain  ${output}  SRM_FILE_PINNED
  ${output}  Perform rf using clientSRM  ${surl}  ${token}  -p
  Should Contain  ${output}  SRM_SUCCESS
  Should Not Contain  ${output}  SRM_RELEASED
  Should Not Contain  ${output}  SRM_FAILURE
  ${output}  Perform rm using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  [Teardown]  Clear all credentials

Prepare to get an unexistent file
  [Tags]  storm-client  ptg
  [Setup]  Init certificate and voms proxy  test0  ${VOMSAUTH_SA_VONAME}
  ${filename}  Get a unique name
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${filename}
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_INVALID_PATH
  [Teardown]  Clear all credentials

Check double PtG on a file
  [Tags]  regression  ptg
  [Setup]  Init certificate and voms proxy  test0  ${VOMSAUTH_SA_VONAME}
  ${filename}  Get a unique name
  Put without really putting using clientSRM  ${VOMSAUTH_SA}  ${filename}
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${filename}
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_FILE_PINNED
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_FILE_PINNED
  Perform rm using clientSRM  ${surl}
  [Teardown]  Clear all credentials

Prepare to get multiple existent file
  [Tags]  storm-client  ptg
  [Setup]  Init certificate and voms proxy  test0  ${VOMSAUTH_SA_VONAME}
  ${testdir}  Get a unique name
  ${dirsurl}  Build surl  ${VOMSAUTH_SA}  ${testdir}
  ${output}  Perform mkdir using clientSRM  ${dirsurl}
  Should Contain  ${output}  SRM_SUCCESS
  ${filename}  Create local file
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${testdir}/${filename}_1
  Copy-out file using lcg-utils  ${filename}  ${surl}
  ${surllist} =  Set Variable  ${surl}
  :FOR  ${index}  IN RANGE  2  10
  \		${surl}  Build surl  ${VOMSAUTH_SA}  ${testdir}/${filename}_${index}
  \		Copy-out file using lcg-utils  ${filename}  ${surl}
  \		${surllist}  Catenate  ${surllist}  ${surl}
  \		Log  ${surllist}
  ${output}  ${token}  Perform ptg using clientSRM  ${surllist}  -p
  Log  ${output}
  Should Contain  ${output}  SRM_FILE_PINNED
  Should Not Contain  ${output}  SRM_FAILURE
  ${output}  Perform rf using clientSRM without token  ${surllist}
  Log  ${output}
  Should Contain  ${output}  SRM_SUCCESS
  Should Not Contain  ${output}  SRM_RELEASED
  Should Not Contain  ${output}  SRM_FAILURE
  ${output}  Perform rmdir using clientSRM  ${dirsurl}  -r
  Should Contain  ${output}  SRM_SUCCESS
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

Prepare to get large file
  [Documentation]  Regression test for https://issues.infn.it/jira/browse/STOR-331
  [Tags]  storm-client  ptg  to-be-fixed
  [Setup]  Init certificate and voms proxy  test0  ${VOMSAUTH_SA_VONAME}
  ${filename}  Create local file with fake size  2049
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${filename}
  ${output}  ${token}  Perform ptp using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_SPACE_AVAILABLE
  Copy-out file using globus-utils  ${filename}  ${VOMSAUTH_SA}  ${filename}
  ${output}  Perform pd using clientSRM  ${surl}  ${token}  -p
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_FILE_PINNED
  Log  ${output}
  Should Contain  ${output}  fileSize=2148532224
  Remove local file  ${filename}
  Perform rf using clientSRM  ${surl}  ${token}
  Perform rm using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  [Teardown]  Clear all credentials

Unauthorized prepare to get on another storage-area's file
  [Tags]  storm-client  ls
  [Setup]  Init certificate and voms proxy  test0  ${VOMSAUTH_SA_VONAME}
  ${filename}  Create local file
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${filename}
  Copy-out file using lcg-utils  ${filename}  ${surl}
  Init certificate and plain proxy  test0
  ${surl}  Build surl  ${X509AUTH_SA}  ../${VOMSAUTH_SA}/${filename}
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  Log  ${output}
  Clear all credentials
  Init certificate and voms proxy  test0  ${VOMSAUTH_SA_VONAME}
  Remove file using clientSRM  ${VOMSAUTH_SA}  ${filename}
  Remove local file  ${filename}
  [Teardown]  Clear all credentials