*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/dcache.txt
Resource   lib/lcg_util.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt
Suite Setup       Create proxies  ${root}
Suite Teardown    Delete proxies

*** Test Cases ***

List files in storage area root
  [Tags]  storm-client  ls
  ${surl}  Build surl  ${root}  ${EMPTY}
  Perform ls using clientSRM  ${surl}  -c 1

Create and remove a directory using clientSRM
  [Tags]  storm-client  rmdir
  ${dirName}  Get a unique name
  Create directory using clientSRM  ${root}  ${dirName}
  Remove empty directory using clientSRM  ${root}  ${dirName}

Create and remove a directory using the dCache client
  [Tags]  dcache-client  rmdir
  ${dirName}  Get a unique name
  Create directory using dCache client  ${root}  ${dirName}
  ${output}  Remove empty directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_SUCCESS
	
Create a directory that already exists using clientSRM
  [Tags]  storm-client  mkdir
  ${dirName}  Get a unique name
  ${output}  Create directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Create directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_DUPLICATION_ERROR
  Should Contain  ${output}  Directory specified exists!
  [Teardown]  Remove empty directory using clientSRM  ${root}  ${dirName}

Create a directory that already exists using the dCache client
  [Tags]  dcache-client  mkdir
  ${dirName}  Get a unique name
  ${output}  Create directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Try to create directory using dCache client  ${root}  ${dirName}
  Should Contain  ${output}  SRM_DUPLICATION_ERROR
  Should Contain  ${output}  Directory specified exists!
  [Teardown]  Remove empty directory using clientSRM  ${root}  ${dirName}

Remove a non existent directory using clientSRM
  [Tags]  storm-client  rmdir
  ${dirName}  Get a unique name
  ${output}  Remove empty directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_INVALID_PATH
  Should Contain  ${output}  Directory does not exists

Remove a non existent directory using the dCache client
  [Tags]  dcache-client  rmdir
  ${dirName}  Get a unique name
  ${output}  Try to remove directory using dCache client  ${root}  ${dirName}
  Should Contain  ${output}  SRM_INVALID_PATH
  Should Contain  ${output}  Directory does not exists

Remove a non existent file using clientSRM
  [Tags]  storm-client  rm
  ${fileName}  Get a unique name
  ${output}  Remove file using clientSRM  ${root}  ${fileName}
  Should Contain  ${output}  SRM_INVALID_PATH
  Should Contain  ${output}  File does not exist

Remove a non existent file using the dCache client
  [Tags]  dcache-client  rm
  ${fileName}  Get a unique name
  ${output}  Try to remove file using dCache client  ${root}  ${fileName}
  Should Contain  ${output}  SRM_INVALID_PATH
  Should Contain  ${output}  File does not exist

List files in an existing directory
  [Tags]  storm-client  lcg-utils  ls
  ${dirName}  Get a unique name
  ${surl}  Build surl  ${root}  ${dirName}
  ${output}  Create directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_SUCCESS
  ${fileName}  Get a unique name
  Put without really putting using clientSRM  ${root}  ${dirName}/${fileName}
  ${output}  List files in directory using lcg_utils  ${root}  ${dirName}
  Should Contain  ${output}  ${fileName}
  ${output}  List files in directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  ${fileName}
  [Teardown]  Remove not empty directory using clientSRM  ${root}  ${dirName}

Check if a clientSRM ls on an existent surl works
  [Tags]  storm-client  ls
  ${filename}  Get a unique name
  Put without really putting using clientSRM  ${root}  ${filename}
  ${surl}  Build surl  ${root}  ${filename}
  ${output}  Perform ls using clientSRM  ${surl}  -c 1
  Should Contain  ${output}  SRM_SUCCESS
  [Teardown]  Perform rm using clientSRM  ${surl}

Check if a clientSRM ls on a non-existent file fails
  [Tags]  storm-client  ls
  ${filename}  Get a unique name
  ${surl}  Build surl  ${root}  ${filename}
  ${output}  Perform ls using clientSRM  ${surl}  -c 1
  Should Contain  ${output}  SRM_FAILURE

Check if a lcg-ls on an existent surl works
  [Tags]  lcg-utils  ls
  ${filename}  Get a unique name
  Put without really putting using clientSRM  ${root}  ${filename}
  ${surl}  Build surl  ${root}  ${filename}
  Check file exists using lcg-utils  ${surl}
  [Teardown]  Remove file using clientSRM  ${root}  ${filename}

Check if a lcg-ls on a non-existent surl fails
  [Tags]  lcg-utils  ls
  ${filename}  Get a unique name
  ${surl}  Build surl  ${root}  ${filename}
  Check file does not exists using lcg-utils  ${surl}

Remove an existent file using dCache client
  [Tags]  dcache-client  rm
  ${filename}  Create local file
  ${surl}  Build surl  ${root}  ${filename}
  Check file does not exists using lcg-utils  ${surl}
  Copy-out file using lcg-utils  ${filename}  ${surl}
  Check file exists using lcg-utils  ${surl}
  Remove file using dCache client  ${root}  ${filename}

Check a clientSRM ls on a non-existent dir fails
  [Tags]  storm-client  ls
  ${dirName}  Get a unique name
  ${output}  List files in directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_FAILURE

Check if a clientSRM rm on an existent file works
  [Tags]  storm-client  rm
  ${filename}  Get a unique name
  ${dirName}  Get a unique name
  ${output}  List files in directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_FAILURE
  ${output}  Create directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_SUCCESS
  ${surl}  Build surl  ${root}  ${dirName}/${filename}
  ${output}  ${token}  Perform ptp using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_SPACE_AVAILABLE
  ${output}  List files in directory using clientSRM  ${root}  ${dirName}
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform rm using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  [Teardown]  Remove not empty directory using clientSRM  ${root}  ${dirName}
