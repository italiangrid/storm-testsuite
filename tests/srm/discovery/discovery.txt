*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/dcache.txt
Resource   lib/lcg_util.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

*** Test Cases ***

Ping using clientSRM
  [Tags]  ping  storm-client
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${output}  Execute clientSRM Command  ping
  Should contain  ${output}  SRM server successfully contacted
  [Teardown]  Clear all credentials

Ping using the dCache client
  [Tags]  ping  dcache-client
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${output}  Ping using dCache client
  Should Contain  ${output}  StoRM
  [Teardown]  Clear all credentials

Ping returns a wrong backend age
  [Tags]  ping  storm-client
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${output}  Execute clientSRM Command  ping
  ${matched}  Should Match Regexp  ${output}  <BE:.+>
  Should Not Contain  ${matched}  age
  [Teardown]  Clear all credentials

Verify gtp operation by using the StoRM client
  [Tags]  gtp  storm-client
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${output}  Execute clientSRM Command  gtp
  Should Contain  ${output}  transferProtocol="file"
  Should Contain  ${output}  transferProtocol="gsiftp"
  Should Contain  ${output}  transferProtocol="http"
  Should Contain  ${output}  transferProtocol="https"
  Should Contain  ${output}  transferProtocol="rfio"
  Should Contain  ${output}  transferProtocol="root"
  [Teardown]  Clear all credentials

Verify asynch request with non-ascii characters
  [Documentation]  Regression test for https://storm.cnaf.infn.it:8443/redmine/issues/137. Given a StoRM endpoint, verify that an SRM asynchronous call providing a string parameter containing non ASCII characters does not put StoRM Frontend offline.
  [Tags]  storm-client  ping  regression
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  catenate  Get a unique name  èèèè
  ${output}  Execute clientSRM Command on Surl  ls -c 1  ${filename}
  Should Not Contain  ${output}  SRM_SUCCESS
  ${output}  Execute clientSRM Command  ping
  Should contain  ${output}  SRM server successfully contacted
  [Teardown]  Clear all credentials