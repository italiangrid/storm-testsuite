*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/clientSRM.txt
Resource   lib/dcache.txt
Resource   lib/lcg_util.txt
Resource   lib/credentials.txt

Resource   lib/setup.txt

Suite Setup       Setup suite if needed
Suite Teardown    Teardown suite if needed

*** Test Cases ***

Check if StoRM publishes correctly values for used and free space on the BDII
  [Tags]  regression
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${filename}  Create local file
  ${surl}  Build surl  ${VOMSAUTH_SA}  ${TESTDIR}/${filename}
  Check file does not exists using lcg-utils  ${surl}
  Copy-out file using lcg-utils  ${filename}  ${surl}
  ${size_before}  Get unused size using clientSRM  ${VOMSAUTH_SA_TOKEN}
  Log  ${size_before}
  Check file exists using lcg-utils  ${surl}
  Perform rm using clientSRM  ${surl}
  Check file does not exists using lcg-utils  ${surl}
  ${size_after}  Get unused size using clientSRM  ${VOMSAUTH_SA_TOKEN}
  Log  ${size_after}
  ${diff_size}  Run  echo `expr ${size_after} - ${size_before}`
  ${file_size}  Run  ls -l /tmp/${TESTDIR}/${filename} | awk {'print $5'}
  Should be equal  ${file_size}  ${diff_size}
  [Teardown]  Clear all credentials

Status of bring online using a non existent token
  [Tags]  regression  sbol
  [Documentation]  Regression test for https://issues.infn.it/browse/STOR-217. StoRM crashed when a status for an async request was requested but the service is not able to get the into out of the database (for example, the token does not exists). This test calls a status for bring online.
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${output}  Execute clientSRM Command  bol -t abcde
  [Teardown]  Clear all credentials
