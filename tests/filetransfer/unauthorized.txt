*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt
Resource   lib/lcg_util.txt

*** Test Cases ***

File-Transfer upload file as anonymous without ptp
  [Tags]  filetransfer  put
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Get a unique name
  ${options}  Set variable  --verbose -X PUT
  ${turl}  Build file transfer TURL  ${anonymousRWStorageArea}  ${filename}
  ${output}  Execute Curl with options  ${turl}  ${options}
  Should Contain  ${output}  401 Unauthorized
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

File-Transfer get file as anonymous without ptg
  [Tags]  filetransfer  get
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${surl}  Build surl  ${anonymousRWStorageArea}  ${filename}
  Copy-out file using lcg-utils  ${filename}  ${surl}
  ${options}  Set variable  --verbose -X GET
  ${turl}  Build file transfer TURL  ${anonymousRWStorageArea}  ${filename}
  ${output}  Execute Curl with options  ${turl}  ${options}
  Should Contain  ${output}  200 OK
  ${output}  Perform rm using clientSRM  ${surl}
  Should contain  ${output}  SRM_SUCCESS
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

File-Transfer get file over https with not sufficient credentials
  [Tags]  filetransfer  get
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${surl}  Build surl  ${vomsproxyRWStorageArea}  ${filename}
  Copy-out file using lcg-utils  ${filename}  ${surl}
  Clear all credentials
  Init certificate with unencrypted key  test0
  ${options}  Set variable  --verbose
  ${turl}  Build file transfer secure TURL  ${vomsproxyRWStorageArea}  ${filename}
  Log  ${turl}
  ${output}  Execute Curl with certificate and options  ${turl}  ${options}
  Log  ${output}
  Should Contain  ${output}  403 You are not authorized to access the requested resource
  Clear all credentials
  Init certificate and plain proxy  test0
  ${output}  Execute Curl with proxy and options  ${turl}  ${options}
  Log  ${output}
  Should Contain  ${output}  403 You are not authorized to access the requested resource
  Clear all credentials
  ${turl}  Build file transfer TURL  ${vomsproxyRWStorageArea}  ${filename}
  Log  ${turl}
  ${output}  Execute Curl with options  ${turl}  ${options}
  Log  ${output}
  Should Contain  ${output}  403 Unauthorized: Anonymous users are not authorized to read
  Init certificate and voms proxy  test0  ${vo}
  ${output}  Perform rm using clientSRM  ${surl}
  Should contain  ${output}  SRM_SUCCESS
  Remove local file  ${filename}
  [Teardown]  Clear all credentials

File-Transfer put file over https with not sufficient credentials
  [Tags]  filetransfer  put
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${surl}  Build surl  ${vomsproxyRWStorageArea}  ${filename}
  Clear all credentials
  Init certificate with unencrypted key  test0
  ${options}  Set variable  --verbose -T /tmp/${filename}
  ${turl}  Build file transfer secure TURL  ${vomsproxyRWStorageArea}  ${filename}
  Log  ${turl}
  ${output}  Execute Curl with certificate and options  ${turl}  ${options}
  Log  ${output}
  Should Contain  ${output}  403 You are not authorized to access the requested resource
  Clear all credentials
  Init certificate and plain proxy  test0
  ${output}  Execute Curl with proxy and options  ${turl}  ${options}
  Log  ${output}
  Should Contain  ${output}  403 You are not authorized to access the requested resource
  Clear all credentials
  ${turl}  Build file transfer TURL  ${vomsproxyRWStorageArea}  ${filename}
  Log  ${turl}
  ${output}  Execute Curl with options  ${turl}  ${options}
  Log  ${output}
  Should Contain  ${output}  403 Unauthorized: Anonymous users are not authorized to read
  Remove local file  ${filename}
  [Teardown]  Clear all credentials