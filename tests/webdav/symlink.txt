*** Settings ***

Resource   lib/webdav.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

Resource   lib/setup.txt

Suite Setup       Setup suite if needed
Suite Teardown    Teardown suite if needed

*** Test Cases ***

WebDAV GET symlink from anonymous SA to voms SA
  [Tags]  webdav  symlink
  [Setup]  Clear all credentials
  ${output}  WebDAV GET  ${davEndpoint}  ${ANONYMOUS_SA}  ${ANONYMOUS_TO_VOMS}
  Log  ${output}
  Should Not Contain  ${output}  200 OK
  Should Contain  ${output}  403
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  ${output}  WebDAV GET  ${davSecureEndpoint}  ${ANONYMOUS_SA}  ${ANONYMOUS_TO_VOMS}  ${vomsproxy}
  Log  ${output}
  Should Contain  ${output}  200 OK
  [Teardown]  Clear all credentials

WebDAV GET symlink from voms SA to anonymous SA
  [Tags]  webdav  symlink
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  ${output}  WebDAV GET  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${VOMS_TO_ANONYMOUS}  ${vomsproxy}
  Log  ${output}
  Should Contain  ${output}  200 OK
  [Teardown]  Clear all credentials

WebDAV GET symlink from voms SA to a different voms SA
  [Tags]  webdav  symlink
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  ${output}  WebDAV GET  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${VOMS_TO_VOMS}  ${vomsproxy}
  Log  ${output}
  Should Not Contain  ${output}  200 OK
  Should Contain  ${output}  403
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  [Teardown]  Clear all credentials

WebDAV GET dotted path from anonymous SA to voms SA
  [Tags]  webdav  symlink
  [Setup]  Clear all credentials
  ${output}  WebDAV GET  ${davEndpoint}  ${ANONYMOUS_SA}  ../${VOMSAUTH_SA}
  Log  ${output}
  Should Not Contain  ${output}  200 OK
  Should Contain  ${output}  403
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  ${output}  WebDAV GET  ${davSecureEndpoint}  ${ANONYMOUS_SA}  ../${VOMSAUTH_SA}  ${vomsproxy}
  Log  ${output}
  Should Contain  ${output}  200 OK
  [Teardown]  Clear all credentials

WebDAV GET dotted path from voms SA to anonymous SA
  [Tags]  webdav  symlink
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  ${output}  WebDAV GET  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ../${ANONYMOUS_SA}  ${vomsproxy}
  Log  ${output}
  Should Contain  ${output}  200 OK
  [Teardown]  Clear all credentials

WebDAV GET dotted path from voms SA to a different voms SA
  [Tags]  webdav  symlink
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  ${output}  WebDAV GET  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ../${VOMSAUTH_SA2}  ${vomsproxy}
  Log  ${output}
  Should Not Contain  ${output}  200 OK
  Should Contain  ${output}  403
  Should Contain  ${output}  SRM_AUTHORIZATION_FAILURE
  [Teardown]  Clear all credentials