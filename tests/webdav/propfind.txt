*** Settings ***

Resource   lib/webdav.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

Resource   lib/setup.txt

Suite Setup       Setup suite if needed
Suite Teardown    Teardown suite if needed

*** Keywords ***

WebDAV PROPFIND allprop on file  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Upload file  ${endpoint}  ${storageArea}  ${credentials}  ${TESTDIR}  test123456789
  ${bodyvalue}  Get PROPFIND ALLPROP body
  ${output}  WebDAV PROPFIND  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${bodyvalue}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  207 Multi-status
  Should Contain  ${output}  ${TESTDIR}/${filename}
  Should Contain  ${output}  <d:checksumType>ADLER32</d:checksumType>
  Should Contain  ${output}  <d:checksumValue>
  Should Contain  ${output}  <d:status>SRM_SUCCESS: Successful request completion</d:status>
  Should Contain  ${output}  <d:iscollection>FALSE</d:iscollection>
  [Return]  ${output}

WebDAV PROPFIND allprop on non empty directory  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${dirname}  Get a unique name
  ${output}  WebDAV MKCOL  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Should Contain  ${output}  201 Created
  ${subFile1}  Upload file  ${endpoint}  ${storageArea}  ${credentials}  ${TESTDIR}/${dirname}
  ${subFile2}  Upload file  ${endpoint}  ${storageArea}  ${credentials}  ${TESTDIR}/${dirname}
  ${bodyvalue}  Get PROPFIND ALLPROP body
  ${output}  WebDAV PROPFIND  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}  ${bodyvalue}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  207 Multi-status
  Should Contain  ${output}  ${TESTDIR}/${dirname}
  Should Contain  ${output}  ${TESTDIR}/${dirname}/${subFile1}
  Should Contain  ${output}  ${TESTDIR}/${dirname}/${subFile2}
  Should Contain  ${output}  <d:checksumType>ADLER32</d:checksumType>
  Should Contain  ${output}  <d:checksumValue>
  Should Contain  ${output}  <d:status>SRM_SUCCESS: Successful request completion</d:status>
  Should Contain  ${output}  <d:iscollection>FALSE</d:iscollection>
  Should Contain  ${output}  <d:iscollection>TRUE</d:iscollection>
  [Return]  ${output}

WebDAV PROPFIND propname on file  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Upload file  ${endpoint}  ${storageArea}  ${credentials}
  ${bodyvalue}  Get PROPFIND PROPNAME body
  ${output}  WebDAV PROPFIND  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${bodyvalue}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  207 Multi-status
  [Return]  ${output}

WebDAV PROPFIND prop on file  [Arguments]  ${endpoint}  ${storageArea}  ${propname}  ${credentials}=${EMPTY}
  ${filename}  Upload file  ${endpoint}  ${storageArea}  ${credentials}
  ${bodyvalue}  Get PROPFIND PROP body  ${propname}
  ${output}  WebDAV PROPFIND  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${bodyvalue}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  207 Multi-status
  [Return]  ${output}

*** Test Cases ***

######### ANONYMOUS ##########

WebDAV PROPFIND allprop on file as anonymous
  [Tags]  webdav  propfind  anonymous
  [Setup]  Clear all credentials
  WebDAV PROPFIND allprop on file  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV PROPFIND allprop on non empty directory as anonymous
  [Tags]  webdav  propfind  anonymous
  [Setup]  Clear all credentials
  WebDAV PROPFIND allprop on non empty directory  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV PROPFIND propname on file as anonymous
  [Tags]  webdav  propfind  anonymous
  [Setup]  Clear all credentials
  WebDAV PROPFIND propname on file  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV PROPFIND prop on file as anonymous
  [Tags]  webdav  propfind  anonymous
  [Setup]  Clear all credentials
  WebDAV PROPFIND prop on file  ${davEndpoint}  ${ANONYMOUS_SA}  status

######### VOMS PROXY ##########

WebDAV PROPFIND allprop on file with VOMS proxy
  [Tags]  webdav  propfind  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV PROPFIND allprop on file  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV PROPFIND allprop on non empty directory with VOMS proxy
  [Tags]  webdav  propfind  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV PROPFIND allprop on non empty directory  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

######### PLAIN PROXY ##########

WebDAV PROPFIND allprop on file with plain proxy
  [Tags]  webdav  propfind  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV PROPFIND allprop on file  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV PROPFIND allprop on non empty directory with plain proxy
  [Tags]  webdav  propfind  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV PROPFIND allprop on non empty directory  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials