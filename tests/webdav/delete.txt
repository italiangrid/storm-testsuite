*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

*** Test Cases ***

WebDAV DELETE file as anonymous
  [Tags]  webdav  rm
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Get a unique name
  Put without really putting using clientSRM  ${anonymousRWStorageArea}  ${filename}
  ${options}  Set variable  --verbose -X DELETE
  ${url}  Set variable  http://${davEndpoint}/${anonymousRWStorageArea}/${filename}
  ${output}  Execute Curl with options  ${url}  ${options}
  Should Contain  ${output}  204 No Content
  [Teardown]  Clear all credentials

WebDAV DELETE empty directory as anonymous
  [Tags]  webdav  rmdir
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${surl}  Build surl  ${anonymousRWStorageArea}  ${dirName}
  ${output}  Perform mkdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  ${options}  Set variable  --verbose -X DELETE
  ${url}  Set variable  http://${davEndpoint}/${anonymousRWStorageArea}/${dirName}
  ${output}  Execute Curl with options  ${url}  ${options}
  Should Contain  ${output}  204 No Content
  [Teardown]  Clear all credentials

WebDAV DELETE full directory as anonymous
  [Tags]  webdav  rmdir
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${dirname}  Get a unique name
  ${surl}  Build surl  ${anonymousRWStorageArea}  ${dirName}
  ${output}  Perform mkdir using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  ${filename}  Get a unique name
  Put without really putting using clientSRM  ${anonymousRWStorageArea}  ${dirName}/${filename}
  ${options}  Set variable  --verbose -X DELETE
  ${url}  Set variable  http://${davEndpoint}/${anonymousRWStorageArea}/${dirName}
  ${output}  Execute Curl with options  ${url}  ${options}
  Should Contain  ${output}  204 No Content
  [Teardown]  Clear all credentials
