*** Settings ***

Resource   lib/webdav.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

*** Keywords ***

WebDAV DELETE file  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Upload file  ${endpoint}  ${storageArea}  ${credentials}
  ${output}  WebDAV DELETE  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  204 No Content
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  404 Not Found

WebDAV DELETE directory  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${dirname}  Get a unique name
  ${output}  WebDAV MKCOL  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  201 Created
  ${filename}  Upload file  ${endpoint}  ${storageArea}  ${credentials}  ${TESTDIR}/${dirname}
  ${output}  WebDAV DELETE  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  204 No Content
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  404 Not Found

WebDAV DELETE unexistent resource  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Get a unique name
  ${output}  WebDAV DELETE  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  404 Not Found

*** Test Cases ***

######### ANONYMOUS ##########

WebDAV DELETE file as anonymous
  [Tags]  webdav  delete  anonymous
  WebDAV DELETE file  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV DELETE directory as anonymous
  [Tags]  webdav  delete  anonymous
  WebDAV DELETE directory  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV DELETE unexistent resource as anonymous
  [Tags]  webdav  delete  anonymous  to-be-fixed
  WebDAV DELETE unexistent resource  ${davEndpoint}  ${ANONYMOUS_SA}

####### VOMS PROXY #########

WebDAV DELETE file with VOMS proxy
  [Tags]  webdav  delete  vomsproxy
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV DELETE file  ${davSecureEndpoint}  ${VOMSPROXY_SA}  ${vomsproxy}

WebDAV DELETE directory with VOMS proxy
  [Tags]  webdav  delete  vomsproxy
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV DELETE directory  ${davSecureEndpoint}  ${VOMSPROXY_SA}  ${vomsproxy}

WebDAV DELETE unexistent resource with VOMS proxy
  [Tags]  webdav  delete  vomsproxy  to-be-fixed
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV DELETE unexistent resource  ${davSecureEndpoint}  ${VOMSPROXY_SA}  ${vomsproxy}

######### PLAIN PROXY ##########

WebDAV DELETE file with plain proxy
  [Tags]  webdav  delete  plainproxy
  ${plainproxy}  Get WebDAV plain proxy credentials
  WebDAV DELETE file  ${davSecureEndpoint}  ${PLAINPROXY_SA}  ${plainproxy}

WebDAV DELETE directory with plain proxy
  [Tags]  webdav  delete  plainproxy
  ${plainproxy}  Get WebDAV plain proxy credentials
  WebDAV DELETE directory  ${davSecureEndpoint}  ${PLAINPROXY_SA}  ${plainproxy}

WebDAV DELETE unexistent resource with plain proxy
  [Tags]  webdav  delete  plainproxy  to-be-fixed
  ${plainproxy}  Get WebDAV plain proxy credentials
  WebDAV DELETE unexistent resource  ${davSecureEndpoint}  ${PLAINPROXY_SA}  ${plainproxy}