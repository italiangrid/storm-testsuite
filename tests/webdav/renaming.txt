*** Settings ***

Resource   lib/webdav.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

Resource   lib/setup.txt

Suite Setup       Setup suite if needed
Suite Teardown    Teardown suite if needed

*** Keywords ***

File renaming  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Upload file  ${endpoint}  ${storageArea}  ${credentials}
  ${destL1dir}  Get a unique name
  ${destL2dir}  Get a unique name
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Should Contain  ${output}  200 OK
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${destL1dir}/${destL2dir}/${filename}  ${credentials}
  Should Contain  ${output}  404 Not Found
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${destL1dir}/${destL2dir}  ${credentials}
  Should Contain  ${output}  404 Not Found
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${destL1dir}  ${credentials}
  Should Contain  ${output}  404 Not Found
  ${output}  WebDAV MKCOL  ${endpoint}  ${storageArea}  ${TESTDIR}/${destL1dir}  ${credentials}
  Should Contain  ${output}  201 Created
  ${output}  WebDAV MKCOL  ${endpoint}  ${storageArea}  ${TESTDIR}/${destL1dir}/${destL2dir}  ${credentials}
  Should Contain  ${output}  201 Created
  ${output}  WebDAV MOVE  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${endpoint}  ${storageArea}  ${TESTDIR}/${destL1dir}/${destL2dir}/${filename}  T  ${credentials}
  Should Contain  ${output}  201 Created

*** Test Cases ***

File renaming as anonymous
  [Tags]  webdav  renaming  anonymous
  [Setup]  Clear all credentials
  File renaming  ${davEndpoint}  ${ANONYMOUS_SA}

File renaming with VOMS proxy
  [Tags]  webdav  renaming  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  File renaming  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

File renaming with plain proxy
  [Tags]  webdav  renaming  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  File renaming  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials