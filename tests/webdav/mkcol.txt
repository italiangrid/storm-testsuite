*** Settings ***

Resource   lib/webdav.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

Resource   lib/setup.txt

Suite Setup       Setup suite if needed
Suite Teardown    Teardown suite if needed

*** Keywords ***

WebDAV MKCOL on unexistent resource  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${dirname}  Get a unique name
  ${output}  WebDAV MKCOL  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  201 Created
  [Return]  ${output}

WebDAV MKCOL with missing anchestor  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${dirname}  Get a unique name
  ${dirnameParent}  Get a unique name
  ${output}  WebDAV MKCOL  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirnameParent}/${dirname}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  409 Conflict
  [Return]  ${output}

WebDAV MKCOL on existent resource  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${dirname}  Get a unique name
  ${output}  WebDAV MKCOL  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  201 Created
  ${output}  WebDAV MKCOL  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  405 Method Not Allowed
  [Return]  ${output}

WebDAV MKCOL unauthorized  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${dirname}  Get a unique name
  ${output}  WebDAV MKCOL  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  403
  [Return]  ${output}


*** Test Cases ***

######### ANONYMOUS ##########

WebDAV MKCOL as anonymous
  [Tags]  webdav  mkcol  anonymous
  [Setup]  Clear all credentials
  WebDAV MKCOL on unexistent resource  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV MKCOL with missing anchestor as anonymous
  [Tags]  webdav  mkcol  anonymous
  [Setup]  Clear all credentials
  WebDAV MKCOL with missing anchestor  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV MKCOL on existent resource as anonymous
  [Tags]  webdav  mkcol  anonymous
  [Setup]  Clear all credentials
  WebDAV MKCOL on existent resource  ${davEndpoint}  ${ANONYMOUS_SA}

####### VOMS PROXY #########

WebDAV MKCOL with VOMS proxy
  [Tags]  webdav  mkcol  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL on unexistent resource  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL with missing anchestor with VOMS proxy
  [Tags]  webdav  mkcol  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL with missing anchestor  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL on existent resource with VOMS proxy
  [Tags]  webdav  mkcol  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL on existent resource  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL with VOMS proxy on an anonymous writeable storage area
  [Tags]  webdav  mkcol  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL on unexistent resource  ${davSecureEndpoint}  ${ANONYMOUS_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

######### PLAIN PROXY ##########

WebDAV MKCOL with plain proxy
  [Tags]  webdav  mkcol  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MKCOL on unexistent resource  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL with missing anchestor with plain proxy
  [Tags]  webdav  mkcol  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MKCOL with missing anchestor  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL on existent resource with plain proxy
  [Tags]  webdav  mkcol  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MKCOL on existent resource  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL with plain proxy on an anonymous writeable storage area
  [Tags]  webdav  mkcol  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MKCOL on unexistent resource  ${davSecureEndpoint}  ${ANONYMOUS_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

####### NESTED #########

WebDAV MKCOL nested storage area with VOMS proxy
  [Tags]  webdav  mkcol  vomsproxy  nested
  [Setup]  Use certificate and proxy  test0  ${NESTED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL on unexistent resource  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL nested storage area with missing anchestor with VOMS proxy
  [Tags]  webdav  mkcol  vomsproxy  nested
  [Setup]  Use certificate and proxy  test0  ${NESTED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL with missing anchestor  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL nested storage area on existent resource with VOMS proxy
  [Tags]  webdav  mkcol  vomsproxy  nested
  [Setup]  Use certificate and proxy  test0  ${NESTED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL on existent resource  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

####### ALIASED #########

WebDAV MKCOL aliased storage area with VOMS proxy
  [Tags]  webdav  mkcol  vomsproxy  aliased
  [Setup]  Use certificate and proxy  test0  ${ALIASED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL on unexistent resource  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL aliased storage area with missing anchestor with VOMS proxy
  [Tags]  webdav  mkcol  vomsproxy  aliased
  [Setup]  Use certificate and proxy  test0  ${ALIASED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL with missing anchestor  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MKCOL aliased storage area on existent resource with VOMS proxy
  [Tags]  webdav  mkcol  vomsproxy  aliased
  [Setup]  Use certificate and proxy  test0  ${ALIASED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MKCOL on existent resource  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials
