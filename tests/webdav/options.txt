*** Settings ***

Resource   lib/webdav.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

Resource   lib/setup.txt

Suite Setup       Setup suite if needed
Suite Teardown    Teardown suite if needed

*** Keywords ***

WebDAV OPTIONS on storage area root  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${output}  WebDAV OPTIONS  ${endpoint}  ${storageArea}  ${EMPTY}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  200 OK
  Should Contain  ${output}  DAV: 1

*** Test Cases ***

######### ANONYMOUS ##########

WebDAV OPTIONS as anonymous
  [Tags]  webdav  options  anonymous
  [Setup]  Clear all credentials
  WebDAV OPTIONS on storage area root  ${davEndpoint}  ${ANONYMOUS_SA}

####### VOMS PROXY #########

WebDAV OPTIONS with VOMS proxy
  [Tags]  webdav  options  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV OPTIONS on storage area root  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

######### PLAIN PROXY ##########

WebDAV OPTIONS with plain proxy
  [Tags]  webdav  options  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV OPTIONS on storage area root  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

####### NESTED #########

WebDAV OPTIONS on nested SA
  [Tags]  webdav  options  nested
  [Setup]  Use certificate and proxy  test0  ${NESTED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV OPTIONS on storage area root  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

####### ALIASED #########

WebDAV OPTIONS on aliased SA
  [Tags]  webdav  options  aliased
  [Setup]  Use certificate and proxy  test0  ${ALIASED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV OPTIONS on storage area root  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials