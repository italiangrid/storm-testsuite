*** Settings ***

Resource   lib/webdav.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

Resource   lib/setup.txt

Suite Setup       Setup suite if needed
Suite Teardown    Teardown suite if needed

*** Keywords ***

WebDAV HEAD root directory  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${output}  WebDAV HEAD root  ${endpoint}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  200 OK
  [Return]  ${output}

WebDAV HEAD existent file  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Upload file  ${endpoint}  ${storageArea}  ${credentials}
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  200 OK

WebDAV HEAD unexistent resource  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Get a unique name
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  404 Not Found

WebDAV HEAD existent directory  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  200 OK

WebDAV HEAD resource with missing parent  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${dirname}  Get a unique name
  ${filename}  Get a unique name
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  404 Not Found

WebDAV HEAD busy file  [Arguments]  ${user}  ${endpoint}  ${storageArea}  ${proxy}  ${curlOptions}=${EMPTY}
  ${filename}  Get a unique name
  ${surl}  Build surl  ${storageArea}  ${TESTDIR}/${filename}
  Use certificate and proxy  ${user}  ${proxy}
  ${output}  ${token}  Perform ptp using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_SPACE_AVAILABLE
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${curlOptions}
  Log  ${output}
  Should Contain  ${output}  200 OK
  ${output}  Perform abort file using clientSRM  ${surl}  ${token}
  Should Contain  ${output}  SRM_SUCCESS
  Clear all credentials

*** Test Cases ***

######### ANONYMOUS ##########

WebDAV HEAD root directory as anonymous
  [Tags]  webdav  head  anonymous
  [Setup]  Clear all credentials
  ${output}  WebDAV HEAD root directory  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD existent file as anonymous
  [Tags]  webdav  head  anonymous
  [Setup]  Clear all credentials
  WebDAV HEAD existent file  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD unexistent resource as anonymous
  [Tags]  webdav  head  anonymous
  [Setup]  Clear all credentials
  WebDAV HEAD unexistent resource  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD existent directory as anonymous
  [Tags]  webdav  head  anonymous
  [Setup]  Clear all credentials
  WebDAV HEAD existent directory  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD resource with missing parent as anonymous
  [Tags]  webdav  head  anonymous
  [Setup]  Clear all credentials
  WebDAV HEAD resource with missing parent  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD busy file as anonymous
  [Tags]  webdav  head  anonymous
  [Setup]  Clear all credentials
  WebDAV HEAD busy file  test0  ${davEndpoint}  ${ANONYMOUS_SA}  ${X509AUTH_SA_PROXY}

####### VOMS PROXY #########

WebDAV HEAD root directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  ${output}  WebDAV HEAD root directory  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD existent file with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD existent file  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD unexistent resource with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD unexistent resource  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD existent directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD existent directory  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD resource with missing parent with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD resource with missing parent  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD busy file with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD busy file  test0  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${VOMSAUTH_SA_PROXY}  ${vomsproxy}
  [Teardown]  Clear all credentials

######### PLAIN PROXY ##########

WebDAV HEAD root directory with plain proxy
  [Tags]  webdav  head  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  ${output}  WebDAV HEAD root directory  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD existent file with plain proxy
  [Tags]  webdav  head  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV HEAD existent file  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD unexistent resource with plain proxy
  [Tags]  webdav  head  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV HEAD unexistent resource  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD existent directory with plain proxy
  [Tags]  webdav  head  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV HEAD existent directory  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD resource with missing parent with plain proxy
  [Tags]  webdav  head  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV HEAD resource with missing parent  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD busy file with plain proxy
  [Tags]  webdav  head  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV HEAD busy file  test0  ${davSecureEndpoint}  ${X509AUTH_SA}  ${X509AUTH_SA_PROXY}  ${plainproxy}
  [Teardown]  Clear all credentials

####### NESTED SA #########

WebDAV HEAD nested storage area root directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  [Setup]  Use certificate and proxy  test0  ${NESTED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  ${output}  WebDAV HEAD root directory  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD nested storage area existent file with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  [Setup]  Use certificate and proxy  test0  ${NESTED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD existent file  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD nested storage area unexistent resource with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  [Setup]  Use certificate and proxy  test0  ${NESTED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD unexistent resource  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD nested storage area existent directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  [Setup]  Use certificate and proxy  test0  ${NESTED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD existent directory  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD nested storage area resource with missing parent with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  [Setup]  Use certificate and proxy  test0  ${NESTED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD resource with missing parent  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

####### ALIASED SA #########

WebDAV HEAD aliased storage area root directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  [Setup]  Use certificate and proxy  test0  ${ALIASED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  ${output}  WebDAV HEAD root directory  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD aliased storage area existent file with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  [Setup]  Use certificate and proxy  test0  ${ALIASED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD existent file  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD aliased storage area unexistent resource with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  [Setup]  Use certificate and proxy  test0  ${ALIASED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD unexistent resource  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD aliased storage area existent directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  [Setup]  Use certificate and proxy  test0  ${ALIASED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD existent directory  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV HEAD aliased storage area resource with missing parent with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  [Setup]  Use certificate and proxy  test0  ${ALIASED_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV HEAD resource with missing parent  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials