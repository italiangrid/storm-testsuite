*** Settings ***

Resource   lib/webdav.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

*** Keywords ***

WebDAV HEAD root directory  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${output}  WebDAV HEAD root  ${endpoint}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  200 OK
  [Return]  ${output}

WebDAV HEAD existent file  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Upload file  ${endpoint}  ${storageArea}  ${credentials}
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  200 OK

WebDAV HEAD unexistent resource  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Get a unique name
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  404 Not Found

WebDAV HEAD existent directory  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  200 OK

WebDAV HEAD resource with missing parent  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${dirname}  Get a unique name
  ${filename}  Get a unique name
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${dirname}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  404 Not Found

WebDAV HEAD busy file  [Arguments]  ${endpoint}  ${storageArea}  ${credentials}=${EMPTY}
  ${filename}  Get a unique name
  ${surl}  Build surl  ${storageArea}  ${TESTDIR}/${filename}
  Init certificate and voms proxy  test0  ${vo}
  ${output}  ${token}  Perform ptp using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_SPACE_AVAILABLE
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  200 OK
  ${output}  Perform abort file using clientSRM  ${surl}  ${token}
  Should Contain  ${output}  SRM_SUCCESS
  Clear all credentials

WebDAV HEAD existent file by unauthorized user  [Arguments]  ${endpoint}  ${storageArea}  ${uploadEndpoint}  ${uploadCredentials}  ${credentials}=${EMPTY}
  ${filename}  Upload file  ${uploadEndpoint}  ${storageArea}  ${uploadCredentials}
  ${output}  WebDAV HEAD  ${endpoint}  ${storageArea}  ${TESTDIR}/${filename}  ${credentials}
  Log  ${output}
  Should Contain  ${output}  403

*** Test Cases ***

######### ANONYMOUS ##########

WebDAV HEAD root directory as anonymous
  [Tags]  webdav  head  anonymous
  ${output}  WebDAV HEAD root directory  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD existent file as anonymous
  [Tags]  webdav  head  anonymous
  WebDAV HEAD existent file  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD unexistent resource as anonymous
  [Tags]  webdav  head  anonymous
  WebDAV HEAD unexistent resource  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD existent directory as anonymous
  [Tags]  webdav  head  anonymous
  WebDAV HEAD existent directory  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD resource with missing parent as anonymous
  [Tags]  webdav  head  anonymous
  WebDAV HEAD resource with missing parent  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD busy file as anonymous
  [Tags]  webdav  head  anonymous
  WebDAV HEAD busy file  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV HEAD existent file by unauthorized user as anonymous
  [Tags]  webdav  head  anonymous
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD existent file by unauthorized user  ${davEndpoint}  ${VOMSPROXY_SA}  ${davSecureEndpoint}  ${vomsproxy}

####### VOMS PROXY #########

WebDAV HEAD root directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  ${output}  WebDAV HEAD root directory  ${davSecureEndpoint}  ${VOMSPROXY_SA}  ${vomsproxy}

WebDAV HEAD existent file with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD existent file  ${davSecureEndpoint}  ${VOMSPROXY_SA}  ${vomsproxy}

WebDAV HEAD unexistent resource with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD unexistent resource  ${davSecureEndpoint}  ${VOMSPROXY_SA}  ${vomsproxy}

WebDAV HEAD existent directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD existent directory  ${davSecureEndpoint}  ${VOMSPROXY_SA}  ${vomsproxy}

WebDAV HEAD resource with missing parent with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD resource with missing parent  ${davSecureEndpoint}  ${VOMSPROXY_SA}  ${vomsproxy}

WebDAV HEAD busy file with VOMS proxy
  [Tags]  webdav  head  vomsproxy
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD busy file  ${davSecureEndpoint}  ${VOMSPROXY_SA}  ${vomsproxy}

WebDAV HEAD existent file with VOMS proxy on an anonymous readable storage area
  [Tags]  webdav  head  vomsproxy
  ${filename}  Upload file  ${davEndpoint}  ${ANONYMOUS_SA}  ${EMPTY}  ${TESTDIR}  test123456789
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  ${output}  WebDAV HEAD  ${davSecureEndpoint}  ${ANONYMOUS_SA}  ${TESTDIR}/${filename}  ${vomsproxy}
  Log  ${output}
  Should Contain  ${output}  200 OK

######### PLAIN PROXY ##########

WebDAV HEAD root directory with plain proxy
  [Tags]  webdav  head  plainproxy
  ${plainproxy}  Get WebDAV plain proxy credentials
  ${output}  WebDAV HEAD root directory  ${davSecureEndpoint}  ${PLAINPROXY_SA}  ${plainproxy}

WebDAV HEAD existent file with plain proxy
  [Tags]  webdav  head  plainproxy
  ${plainproxy}  Get WebDAV plain proxy credentials
  WebDAV HEAD existent file  ${davSecureEndpoint}  ${PLAINPROXY_SA}  ${plainproxy}

WebDAV HEAD unexistent resource with plain proxy
  [Tags]  webdav  head  plainproxy
  ${plainproxy}  Get WebDAV plain proxy credentials
  WebDAV HEAD unexistent resource  ${davSecureEndpoint}  ${PLAINPROXY_SA}  ${plainproxy}

WebDAV HEAD existent directory with plain proxy
  [Tags]  webdav  head  plainproxy
  ${plainproxy}  Get WebDAV plain proxy credentials
  WebDAV HEAD existent directory  ${davSecureEndpoint}  ${PLAINPROXY_SA}  ${plainproxy}

WebDAV HEAD resource with missing parent with plain proxy
  [Tags]  webdav  head  plainproxy
  ${plainproxy}  Get WebDAV plain proxy credentials
  WebDAV HEAD resource with missing parent  ${davSecureEndpoint}  ${PLAINPROXY_SA}  ${plainproxy}

WebDAV HEAD busy file with plain proxy
  [Tags]  webdav  head  plainproxy
  ${plainproxy}  Get WebDAV plain proxy credentials
  WebDAV HEAD busy file  ${davSecureEndpoint}  ${PLAINPROXY_SA}  ${plainproxy}

WebDAV HEAD existent file with plain proxy on an anonymous readable storage area
  [Tags]  webdav  head  plainproxy
  ${filename}  Upload file  ${davEndpoint}  ${ANONYMOUS_SA}  ${EMPTY}  ${TESTDIR}  test123456789
  ${plainproxy}  Get WebDAV plain proxy credentials
  ${output}  WebDAV HEAD  ${davSecureEndpoint}  ${ANONYMOUS_SA}  ${TESTDIR}/${filename}  ${plainproxy}
  Log  ${output}
  Should Contain  ${output}  200 OK

####### NESTED SA #########

WebDAV HEAD nested storage area root directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  ${output}  WebDAV HEAD root directory  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}

WebDAV HEAD nested storage area existent file with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD existent file  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}

WebDAV HEAD nested storage area unexistent resource with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD unexistent resource  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}

WebDAV HEAD nested storage area existent directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD existent directory  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}

WebDAV HEAD nested storage area resource with missing parent with VOMS proxy
  [Tags]  webdav  head  vomsproxy  nested
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD resource with missing parent  ${davSecureEndpoint}  ${NESTED_SA}  ${vomsproxy}

####### ALIASED SA #########

WebDAV HEAD aliased storage area root directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  ${output}  WebDAV HEAD root directory  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}

WebDAV HEAD aliased storage area existent file with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD existent file  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}

WebDAV HEAD aliased storage area unexistent resource with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD unexistent resource  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}

WebDAV HEAD aliased storage area existent directory with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD existent directory  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}

WebDAV HEAD aliased storage area resource with missing parent with VOMS proxy
  [Tags]  webdav  head  vomsproxy  aliased
  ${vomsproxy}  Get WebDAV VOMS proxy credentials
  WebDAV HEAD resource with missing parent  ${davSecureEndpoint}  ${ALIASED_SA}  ${vomsproxy}
