*** Settings ***

Resource   lib/webdav.txt
Resource   lib/clientSRM.txt
Resource   lib/credentials.txt

Resource   lib/setup.txt

Suite Setup       Setup suite if needed
Suite Teardown    Teardown suite if needed

*** Keywords ***

WebDAV MOVE file  [Arguments]  ${srcEndpoint}  ${srcStorageArea}  ${destEndpoint}  ${destStorageArea}  ${credentials}=${EMPTY}
  ${srcFilename}  Upload file  ${srcEndpoint}  ${srcStorageArea}  ${credentials}
  ${destFilename}  Get a unique name
  ${output}  WebDAV MOVE  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${srcFilename}  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destFilename}  T  ${credentials}
  Should Contain  ${output}  201 Created
  ${output}  WebDAV HEAD  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${srcFilename}  ${credentials}
  Should Contain  ${output}  404 Not Found
  ${output}  WebDAV HEAD  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destFilename}  ${credentials}
  Should Contain  ${output}  200 OK

WebDAV MOVE non empty directory  [Arguments]  ${srcEndpoint}  ${srcStorageArea}  ${destEndpoint}  ${destStorageArea}  ${credentials}=${EMPTY}
  ${dirname}  Get a unique name
  ${output}  WebDAV MKCOL  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Should Contain  ${output}  201 Created
  ${subFile1}  Upload file  ${srcEndpoint}  ${srcStorageArea}  ${credentials}  ${TESTDIR}/${dirname}
  ${subFile2}  Upload file  ${srcEndpoint}  ${srcStorageArea}  ${credentials}  ${TESTDIR}/${dirname}
  ${destDirname}  Get a unique name
  ${output}  WebDAV MOVE  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${dirname}  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destDirname}  T  ${credentials}
  Should Contain  ${output}  201 Created
  ${output}  WebDAV HEAD  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${dirname}  ${credentials}
  Should Contain  ${output}  404 Not Found
  ${output}  WebDAV HEAD  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destDirname}  ${credentials}
  Should Contain  ${output}  200 OK
  ${output}  WebDAV HEAD  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destDirname}/${subFile1}  ${credentials}
  Should Contain  ${output}  200 OK
  ${output}  WebDAV HEAD  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destDirname}/${subFile2}  ${credentials}
  Should Contain  ${output}  200 OK

WebDAV MOVE file on existent target with overwrite T  [Arguments]  ${srcEndpoint}  ${srcStorageArea}  ${destEndpoint}  ${destStorageArea}  ${credentials}=${EMPTY}
  ${srcFilename}  Upload file  ${srcEndpoint}  ${srcStorageArea}  ${credentials}
  ${destFilename}  Upload file  ${destEndpoint}  ${destStorageArea}  ${credentials}
  ${output}  WebDAV MOVE  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${srcFilename}  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destFilename}  T  ${credentials}
  Should Contain  ${output}  204 No Content
  ${output}  WebDAV HEAD  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${srcFilename}  ${credentials}
  Should Contain  ${output}  404 Not Found
  ${output}  WebDAV HEAD  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destFilename}  ${credentials}
  Should Contain  ${output}  200 OK

WebDAV MOVE file on existent target with overwrite F  [Arguments]  ${srcEndpoint}  ${srcStorageArea}  ${destEndpoint}  ${destStorageArea}  ${credentials}=${EMPTY}
  ${srcFilename}  Upload file  ${srcEndpoint}  ${srcStorageArea}  ${credentials}
  ${destFilename}  Upload file  ${destEndpoint}  ${destStorageArea}  ${credentials}
  ${output}  WebDAV MOVE  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${srcFilename}  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destFilename}  F  ${credentials}
  Should Contain  ${output}  412 Precondition Failed

WebDAV MOVE unexistent file  [Arguments]  ${srcEndpoint}  ${srcStorageArea}  ${destEndpoint}  ${destStorageArea}  ${credentials}=${EMPTY}
  ${srcFilename}  Get a unique name
  ${destFilename}  Get a unique name
  ${output}  WebDAV MOVE  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${srcFilename}  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destFilename}  T  ${credentials}
  Should Contain  ${output}  404 Not Found

WebDAV MOVE with destination equals to source  [Arguments]  ${srcEndpoint}  ${srcStorageArea}  ${credentials}=${EMPTY}
  ${srcFilename}  Upload file  ${srcEndpoint}  ${srcStorageArea}  ${credentials}
  ${output}  WebDAV MOVE  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${srcFilename}  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${srcFilename}  T  ${credentials}
  Should Contain  ${output}  412

WebDAV MOVE unauthorized  [Arguments]  ${srcEndpoint}  ${srcStorageArea}  ${destEndpoint}  ${destStorageArea}  ${credentials}=${EMPTY}
  ${srcFilename}  Upload file  ${srcEndpoint}  ${srcStorageArea}  ${credentials}
  ${destFilename}  Get a unique name
  ${output}  WebDAV MOVE  ${srcEndpoint}  ${srcStorageArea}  ${TESTDIR}/${srcFilename}  ${destEndpoint}  ${destStorageArea}  ${TESTDIR}/${destFilename}  T  ${credentials}
  Should Contain  ${output}  403

*** Test Cases ***

######### ANONYMOUS ##########

WebDAV MOVE file as anonymous
  [Tags]  webdav  move  anonymous
  [Setup]  Clear all credentials
  WebDAV MOVE file  ${davEndpoint}  ${ANONYMOUS_SA}  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV MOVE non empty directory as anonymous
  [Tags]  webdav  move  anonymous
  [Setup]  Clear all credentials
  WebDAV MOVE non empty directory  ${davEndpoint}  ${ANONYMOUS_SA}  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV MOVE file on existent target with overwrite T as anonymous
  [Tags]  webdav  move  anonymous
  [Setup]  Clear all credentials
  WebDAV MOVE file on existent target with overwrite T  ${davEndpoint}  ${ANONYMOUS_SA}  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV MOVE file on existent target with overwrite F as anonymous
  [Tags]  webdav  move  anonymous
  [Setup]  Clear all credentials
  WebDAV MOVE file on existent target with overwrite F  ${davEndpoint}  ${ANONYMOUS_SA}  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV MOVE unexistent file as anonymous
  [Tags]  webdav  move  anonymous  to-be-fixed
  [Setup]  Clear all credentials
  WebDAV MOVE unexistent file  ${davEndpoint}  ${ANONYMOUS_SA}  ${davEndpoint}  ${ANONYMOUS_SA}

WebDAV MOVE with destination equals to source as anonymous
  [Tags]  webdav  move  anonymous
  [Setup]  Clear all credentials
  WebDAV MOVE with destination equals to source  ${davEndpoint}  ${ANONYMOUS_SA}

######### VOMS PROXY ##########

WebDAV MOVE file with VOMS proxy
  [Tags]  webdav  move  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MOVE file  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE non empty directory with VOMS proxy
  [Tags]  webdav  move  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MOVE non empty directory  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE file on existent target with overwrite T with VOMS proxy
  [Tags]  webdav  move  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MOVE file on existent target with overwrite T  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE file on existent target with overwrite F with VOMS proxy
  [Tags]  webdav  move  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MOVE file on existent target with overwrite F  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE unexistent file with VOMS proxy
  [Tags]  webdav  move  vomsproxy  to-be-fixed
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MOVE unexistent file  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE with destination equals to source with VOMS proxy
  [Tags]  webdav  move  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MOVE with destination equals to source  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

######### PLAIN PROXY ##########

WebDAV MOVE file with plain proxy
  [Tags]  webdav  move  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MOVE file  ${davSecureEndpoint}  ${X509AUTH_SA}  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE non empty directory with plain proxy
  [Tags]  webdav  move  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MOVE non empty directory  ${davSecureEndpoint}  ${X509AUTH_SA}  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE file on existent target with overwrite T with plain proxy
  [Tags]  webdav  move  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MOVE file on existent target with overwrite T  ${davSecureEndpoint}  ${X509AUTH_SA}  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE file on existent target with overwrite F with plain proxy
  [Tags]  webdav  move  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MOVE file on existent target with overwrite F  ${davSecureEndpoint}  ${X509AUTH_SA}  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE unexistent file with plain proxy
  [Tags]  webdav  move  plainproxy  to-be-fixed
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MOVE unexistent file  ${davSecureEndpoint}  ${X509AUTH_SA}  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE with destination equals to source with plain proxy
  [Tags]  webdav  move  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MOVE with destination equals to source  ${davSecureEndpoint}  ${X509AUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

######### MIXEDÂ ##########

WebDAV MOVE from anonymous to voms proxy storage area
  [Tags]  webdav  move  anonymous  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MOVE file  ${davSecureEndpoint}  ${ANONYMOUS_SA}  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE from VOMS proxy to anonymous storage area
  [Tags]  webdav  move  anonymous  vomsproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MOVE file  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${davSecureEndpoint}  ${ANONYMOUS_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE from plain proxy to anonymous storage area
  [Tags]  webdav  move  anonymous  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MOVE file  ${davSecureEndpoint}  ${X509AUTH_SA}  ${davSecureEndpoint}  ${ANONYMOUS_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE from plain proxy to VOMS proxy storage area
  [Tags]  webdav  move  vomsproxy  plainproxy
  [Setup]  Use certificate and proxy  test0  ${VOMSAUTH_SA_PROXY}
  ${vomsproxy}  Get VOMS proxy options for Curl
  WebDAV MOVE file  ${davSecureEndpoint}  ${X509AUTH_SA}  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${vomsproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE unauthorized from plain proxy to VOMS proxy storage area
  [Tags]  webdav  move  vomsproxy  plainproxy
  [Setup]  Use certificate and proxy  test0  ${X509AUTH_SA_PROXY}
  ${plainproxy}  Get x509 options for Curl
  WebDAV MOVE unauthorized  ${davSecureEndpoint}  ${X509AUTH_SA}  ${davSecureEndpoint}  ${VOMSAUTH_SA}  ${plainproxy}
  [Teardown]  Clear all credentials

WebDAV MOVE unauthorized from anonymous to voms proxy storage area
  [Tags]  webdav  move  anonymous
  [Setup]  Clear all credentials
  WebDAV MOVE unauthorized  ${davEndpoint}  ${ANONYMOUS_SA}  ${davSecureEndpoint}  ${VOMSAUTH_SA}
